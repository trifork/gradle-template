// This file is the base build-script for the project. There are additional
// config files in the 'config'-directory, that handle different aspects of
// the build and deployment for the project.

apply plugin: 'java-base'

// Please make sure you read the README.md file before you try to understand
// what is going on.

allprojects {
	// You can use 'gradle eclipse' to generate eclipse project files.
	// You can use 'gradle -t' to see a list of availible tasks.
	apply plugin: 'eclipse'

	defaultTasks 'build'

	// Usually you will want to add proxy repositories to nexus instead on
	// adding explicitly here.
	repositories {
		mavenLocal()
		mavenRepo(url: 'http://nexus.trifork.com/content/groups/public/')
		mavenRepo(url: 'http://nexus.trifork.com/content/groups/public-snapshots/')
	}
}

subprojects { subproject ->
	
	// Subproject currently always default to java. This might change in the future.
	// If you are making WARs take a look at the 'war' plugin.
	apply plugin: 'java'

	targetCompatibility = 1.6
	sourceCompatibility = 1.6
	
	// Include references to common dependencies so you can keep
	// versions consistent accross your sub-projects.
	
	apply from: "${rootDir}/config/libraries.groovy"

	// Checkstyle source coding standards.
	// TODO: These setting are a bit verbose, but
	// the implementation seems to be a bit broken (in Gradle 0.9.2).
	apply plugin: 'code-quality'
	checkstyleConfigDir = "$rootDir/config/checkstyle"
	checkstyleConfigFileName = new File(checkstyleConfigDir, "checkstyle.xml")
	checkstyleProperties.checkstyleConfigDir = checkstyleConfigDir

	// Shared dependencies for all subprojects.
	// Logging is a common shared dependency.
	dependencies {
		testCompile	libraries.junit
	}

	// Add some extra memory for testing, this is usually useful.
	test {
		maxHeapSize = "1024m"
	}
}

dependsOnChildren()

// The following is section is adds the plugins from
// https://github.com/trifork/gradle-plugins.

buildscript {
	repositories {
		mavenLocal()
		mavenRepo name: 'trifork-releases', url: 'http://nexus.trifork.com/content/groups/public/'
		mavenRepo name: 'trifork-snapshots', url: 'http://nexus.trifork.com/content/groups/public-snapshots/'
	}
}

// This task creates a wrapper so people don't have to have
// Gradle installed to build the program. This has already
// been executed and is included in the repo. 

task wrapper(type: Wrapper) {
    gradleVersion = '1.0-milestone-6'
    jarFile = 'config/gradle/wrapper.jar'
}

